plugins {
    id 'org.sonarqube' version '2.6.2'
    id 'java'
    id 'org.springframework.boot' version '2.2.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.openapi.generator' version '4.2.3'
    id 'jacoco'
}

apply plugin: 'org.openapi.generator'

group = 'net.codecrete.qrbill'
version = '2.2.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

bootJar {
    baseName = 'qrbill-service'
    mainClassName = 'net.codecrete.qrbill.web.Application'
}

test {
    useJUnitPlatform()
}

def openapiInput = new File(projectDir, '/src/main/resources/static/qrbill.yaml')
def openapiOutputDir = new File(buildDir, 'openapi')
def openapiSourceDir = new File(openapiOutputDir, 'src/main/java')
def openapiIgnoreFiles = new File(projectDir, '.openapi-generator-ignore')

openApiGenerate {
    generatorName = 'spring'
    inputSpec = openapiInput.path
    outputDir = openapiOutputDir.path
    apiPackage     = 'net.codecrete.qrbill.web.api'
    invokerPackage = 'net.codecrete.qrbill.web.config'
    modelPackage   = 'net.codecrete.qrbill.web.model'
    ignoreFileOverride = openapiIgnoreFiles.path
    configOptions = [
            hideGenerationTimestamp: 'true',
            java8: 'true',
            dateLibrary: 'java8',
            configPackage: 'net.codecrete.qrbill.web.config'
    ]
}

sourceSets {
    main {
        java {
            srcDirs openapiSourceDir
        }
    }
}

dependencies {
    implementation project(':generator')
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'com.squareup.okhttp3:okhttp:4.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

compileJava.dependsOn tasks.openApiGenerate

sonarqube {
    properties {
        property 'sonar.exclusions', 'build/**/*'
    }
}
